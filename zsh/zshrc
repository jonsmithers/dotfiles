print_emojis=1

ZIM_HOME="$HOME/.zim"
setopt auto_cd

# https://github.com/ohmyzsh/ohmyzsh/blob/b6bb133f230847ed0b3f9f4e25f2ceb874ca6c91/lib/history.zsh#L35C1-L40C59
setopt extended_history       # record timestamp of command in HISTFILE
setopt hist_expire_dups_first # delete duplicates first when HISTFILE size exceeds HISTSIZE
setopt hist_ignore_dups       # ignore duplicated commands history list
setopt hist_ignore_space      # ignore commands that start with space
setopt hist_verify            # show command with history expansion to user before running it
setopt share_history          # share command history data
export LESS='-R'

# Download zimfw plugin manager if missing.
if [[ ! -e "${ZIM_HOME}/zimfw.zsh" ]]; then
  echo downloading
  curl -fsSL --create-dirs -o "${ZIM_HOME}/zimfw.zsh" \
      https://github.com/zimfw/zimfw/releases/latest/download/zimfw.zsh
fi
# Install missing modules, and update ${ZIM_HOME}/init.zsh if missing or outdated.
if [[ ! "${ZIM_HOME}/init.zsh" -nt "${ZDOTDIR:-${HOME}}/.zimrc" ]]; then
  echo initting
  source "${ZIM_HOME}/zimfw.zsh" init -q
fi

zstyle ':zim:termtitle' hooks 'preexec' 'precmd'
if [[ "$TERM" == xterm-kitty ]]; then
  # https://dev.to/ifenna__/adding-colors-to-bash-scripts-48g4
  normal='\033[0m'
  bold='\033[1m'
  red='\033[31m'
  grey='\033[90m'
  zstyle ':zim:termtitle:preexec' format 'Û∞ö≠ '"$red"'${${(A)=1}[1]}'"$normal"
  zstyle ':zim:termtitle:precmd'  format 'ÔÑ† %1~'
else
  zstyle ':zim:termtitle:preexec' format '${${(A)=1}[1]}'
  zstyle ':zim:termtitle:precmd'  format '%1~'
fi
stty -ixon # needed for fzf-git.sh plugin

# Initialize modules.
source "${ZIM_HOME}/init.zsh"
[[ $print_emojis ]] && echo -n ü¶Ñ

# Usage: has <command>
#
# Returns 0 if the <command> is available. Returns 1 otherwise. It can be a
# binary in the PATH or a shell function.
#
# Example:
#
#    if has curl; then
#      echo "Yes we do"
#    fi
#
# Taken from https://github.com/direnv/direnv/blob/master/stdlib.sh
has() {
  type "$1" &>/dev/null
}

function silent_background() {
  # https://superuser.com/questions/1228411/silent-background-jobs-in-zsh/1285272#1285272
  setopt local_options no_notify no_monitor
  "$@" &
  disown &>/dev/null
}

[[ -d "/opt/homebrew/bin" ]] && export PATH=/opt/homebrew/bin/:$PATH
[[ -d "$HOME/.local/bin"  ]] && export PATH=$HOME/.local/bin:$PATH
[[ -d "$HOME/.npm-global" ]] && export PATH=$HOME/.npm-global/bin:$PATH
[[ -d "$HOME/.local/bin"  ]] && export PATH=$HOME/.local/bin:$PATH # "pip install --user" installs executables to this location
[[ -d "$HOME/bin"         ]] && export PATH="$HOME/bin:$PATH"
[[ -d "$HOME/.deno/bin"   ]] && export PATH="$HOME/.deno/bin:$PATH"

[[ $print_emojis ]] && echo -n üç∏

[[ $print_emojis ]] && echo -n üêò
setopt complete_aliases
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
if command -v zoxide > /dev/null; then
  eval "$(zoxide init zsh)"
  function c() {
    if [[ -z "$@" ]]; then
      __zoxide_zi
    else
      __zoxide_z "$@"
    fi
  }
  alias j=c
fi
# clock cow
[[ $print_emojis ]] && echo -n üêÇ
if has lsd; then
  alias ls=lsd
  alias ll='lsd --long'
  alias l='lsd --long'
else
  alias ll='ls -alF'
fi
if has delta; then
  alias d='delta'
fi

alias estimate_development_cost='docker run --rm -it -v "$PWD:/pwd"  ghcr.io/lhoupert/scc:master scc /pwd'

local vim_cmd=vim
if has nvim; then
  vim_cmd=nvim
  alias nvimdiff='nvim -d'
  alias viff='v +DiffviewOpen'
  alias gd='v +DiffviewOpen'
fi
if command -v git-number > /dev/null; then
  alias g='git number'
  if has compdef; then
    compdef g=git
  fi
  alias gim="git-number -c $vim_cmd"
  alias giff="git-number -c $vim_cmd +Gvdiff"
fi
# alias gs="$vim_cmd"' +0Git "+normal gU"'
alias gs="nvim +DiffviewOpen"
alias v="$vim_cmd"
alias V="$vim_cmd ."
unset vim_cmd

if [[ "$(uname)" == "Linux" ]]; then
  alias open="xdg-open"
  if ! grep -q Microsoft /proc/version; then
    alias pbcopy='xsel --clipboard --input'
    alias pbpaste='xsel --clipboard --output'
  else
    alias pbcopy='clip.exe'
    alias pbpaste='powershell.exe -command "Get-Clipboard"'
  fi
fi

[[ $print_emojis ]] && echo -ne üê≠

if has fzf; then

  emoji() {
    # https://www.thenegation.com/posts/nix-fzf-script-tutorial/
    [[ -f /tmp/emoji.json ]] || curl -sSL "https://raw.githubusercontent.com/github/gemoji/0eca75db9301421efc8710baf7a7576793ae452a/db/emoji.json" > /tmp/emoji.json
    cat /tmp/emoji.json |
      jq -r '.[] | (.emoji + " :" + .aliases[0] + ": "+ .category + " ¬ª " + .description)' |
      fzf \
        --delimiter " " \
        --bind 'enter:become(printf {1} | pbcopy)' \
        --bind 'ctrl-c:become(printf {2} | pbcopy)'
  }

  fzfCommit() {
    local FZF_PROMPT="${FZF_PROMPT:=Commit: }"
    git log --oneline | fzf --border --prompt="$FZF_PROMPT" --height=10         --preview="git show {+1} --color=always" --no-sort --reverse | cut -d' ' -f1 | tr '\n' ' ' | sed 's/[[:space:]]$//';
  }
  fzfCommits() {
    local FZF_PROMPT="${FZF_PROMPT:=Commit: }"
    git log --oneline | fzf --border --prompt="$FZF_PROMPT" --height=10 --multi --preview="git show {+1} --color=always" --no-sort --reverse | cut -d' ' -f1 | tr '\n' ' ' | sed 's/[[:space:]]$//'
  }
  fzfTags() {
    git tag | grep -v HEAD | sed "s/.* //" | sed "s#remotes/##" | sort -u | fzf --border --prompt='Tag: ' --height=30 --multi --preview="git log --format=medium --graph --max-count 10 {+1}" | tr '\n' ' ' | sed 's/[[:space:]]$//'
  }
  fzfBranches() {
    git branch --all | grep -v HEAD | sed "s/.* //" | sed "s#remotes/##" | sort -u | fzf --border --prompt='Branch: ' --height=60% --multi --preview="git log --graph --max-count=10 {+1}" | tr '\n' ' ' | sed 's/[[:space:]]$//'
  }
  insertCommits() {
    if [[ "$LBUFFER[-1]" != "!" ]]; then
      LBUFFER+='C'
      return 0
    fi
    LBUFFER="${LBUFFER[1,-2]}$(fzfCommits)"
    local ret=$?
    zle reset-prompt
    return $ret
  }
  insertTags() {
    if [[ "$LBUFFER[-1]" != "!" ]]; then
      LBUFFER+='T'
      return 0
    fi
    LBUFFER="${LBUFFER[1,-2]}$(fzfTags)"
    local ret=$?
    zle reset-prompt
    return $ret
  }
  insertBranches() {
    if [[ "$LBUFFER[-1]" != "!" ]]; then
      LBUFFER+='B'
      return 0
    fi
    LBUFFER="${LBUFFER[1,-2]}$(fzfBranches)"
    local ret=$?
    zle reset-prompt
    return $ret
  }

  zle -N insertCommits
  bindkey C insertCommits
  zle -N insertTags
  bindkey T insertTags
  zle -N insertBranches
  bindkey B insertBranches
fi

function frecent-dirs {
  if [[ $BUFFER != "sudo "* ]]; then
    blue='\033[34m'
    lightblue='\033[94m'
    lightgreen='\033[93m'
    zi
    dir="$(pwd)"
    echo -n "${lightblue}ÔÅº ${dir/"$HOME"/~} ${lightgreen}‚ùØ "
  fi
}
zle -N frecent-dirs
bindkey '^n' frecent-dirs

function gfixup {
  local commit=$(FZF_PROMPT='Fixup Commit: ' fzfCommit)
  if [[ -z "$commit" ]]; then
    return 1
  fi
  set -x
  git commit --fixup "$commit" --allow-empty > /dev/null || return 1
  git rebase --interactive "$commit"~ --autosquash || return 1
}

if has git-mob && has fzf; then
  local GIT_MOB_BUT_FASTER="$(has git-mob-but-faster && echo "git-mob-but-faster" || echo "mob")"
  function gmob() {
    local selected=$($GIT_MOB_BUT_FASTER -l \
      | fzf --multi \
        --layout=reverse \
        --height=11 \
        --border=rounded \
        --prompt='ü¶ú Pair with ' \
      | awk '{print $1;}' \
      | tr '\n' ' ' \
      | sed 's/,//g' )
    [[ -n "$selected" ]] && git mob $(echo $selected)
  }
fi

if has _fzf_git_each_ref; then
  function gco() {
    local selected=$(_fzf_git_each_ref --no-multi)
    [[ -n "$selected" ]] && echo git checkout "$selected"
  }
fi

[[ $print_emojis ]] && echo -n ü¶©

if has fnm; then
  eval "$(fnm env --use-on-cd)"
fi

[[ $print_emojis ]] && echo -n ü¶Ä

[[ -d "$HOME/.cargo" ]] && {
  export PATH=$HOME/.cargo/bin:$PATH
}
if [[ -f /proc/version ]] && grep -q Microsoft /proc/version; then
  echo 'this is windows'
fi

if has rg; then
  export FZF_DEFAULT_COMMAND="rg --hidden --files --glob !.git"
  export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
fi
export FZF_DEFAULT_OPTS='
  --info=inline
  --bind ctrl-q:toggle-all
  --bind ctrl-j:preview-down+preview-down+preview-down+preview-down
  --bind ctrl-k:preview-up
  --bind ctrl-u:preview-page-up
# --bind ctrl-d:preview-page-down (need this to delete chars)
  --pointer=‚ûú
  --ellipsis=‚Ä¶
  --marker=‚úì
  --bind ctrl-'"\\\\\\"':toggle-preview
  --bind '"'"'alt-q:change-preview-window(right,70%|down,40%,border-horizontal|hidden|right)'"'"'
  --bind '"'"'alt-Q:change-preview-window(right|hidden|down,40%,border-horizontal|right,70%)'"'"'
  --bind ctrl-y:preview-up
  --bind ctrl-r:prev-history
  --bind ctrl-t:next-history
  --bind ctrl-p:up
  --bind ctrl-n:down
  '
# select previous       | ctrl-p | :-( not show previous history search
# carot to end          | ctrl-e | :-( not scroll down
# carot to start        | ctrl-a |
# preview down          | ctrl-j | :-( not select next
# preview page down     |        |
# preview page up       | ctrl-u |
# preview up            | ctrl-k | :-( not kill line :-( not select previous
# rotate preview layout | alt-Q  |
# rotate preview layout | alt-q  |
# toggle all items      | alt-a  |
# toggle all items      | ctrl-q |
# toggle preview        | ctrl-/ | ??? doesn't work in kitty/nvim
# toggle preview        | ctrl-\ |

[[ $print_emojis ]] && echo -ne üê∂
if has direnv; then
  eval "$(direnv hook zsh)"
fi

if [[ "$TERM" = "xterm-kitty" ]]; then
  alias rg='rg --hyperlink-format=kitty'
  [[ $print_emojis ]] && echo -n üê±
  alias icat="kitty +kitten icat"
  alias hg="kitty +kitten hyperlinked_grep "
  compdef hg=rg
  # https://www.baeldung.com/linux/background-process-get-exit-code

  if has fzf; then
    function fzfimg {
      # https://github.com/junegunn/fzf/pull/3567
      fzf -q '.png$ | .jpg$ | .gif$' --preview 'kitty icat --clear --transfer-mode=memory --stdin=no --place="${FZF_PREVIEW_COLUMNS}x${FZF_PREVIEW_LINES}@0x0" {}'
    }
  fi

  b() {
    str=''
    for arg in "$@"; do
      if [[ "$arg" =~ ' ' ]]; then
        str="$str '$arg'"
      else
        str="$str $arg"
      fi
    done
    echo "$str"

    local this_window="$KITTY_WINDOW_ID"
    local other_window="$(kitty @ launch --cwd=current --location=hsplit --bias=10)"
    # kitty @ send-text --match "id:$other_window" " $@"' \\\r && sleep 2 && exit\n'
    kitty @ send-text --match "id:$other_window" "$str"' \\\r && sleep 2 && exit\n'
    kitty @ focus-window --match "id:$this_window"
  }

  notify() {
    local errcode="$?"
    local title
    local prev_cmd="$(tail -n 1 $HISTFILE | sed 's/^[^;]*;//')"
    if [[ "$errcode" = "0" ]]; then
      title="${1:-‚úÖ $prev_cmd}"
    else
      title="${1:-‚ùå $prev_cmd}"
    fi
    local body="$2"
    printf '\x1b]99;i=1:d=0;'"$title"'\x1b\\'
    printf '\x1b]99;i=1:d=1:p=body;'"$body"'\x1b\\'
    printf '\e[?5h'; sleep 0.1 ; printf '\e[?5l'
    return $errcode
  }
  alias n=notify
  function post_hook_1() {
    local errcode="$?"
    local kitty_window_id="$1"
    if [[ "$errcode" != "0" ]]; then
      kitty @ focus-window --match id:"$kitty_window_id" # kitty seems to only place the overlay on the active window
      kitty @ action show_last_command_output
    else
      echo 'success'
    fi
    # browse on exit
  }
  function post_hook_2() {
    local errcode="$?"
    local kitty_window_id="$1"
    if [[ "$errcode" != "0" ]]; then
      kitty @ focus-window --match id:"$kitty_window_id" # kitty seems to only place the overlay on the active window
      kitty @ action move_window_to_top
      kitty @ action show_last_command_output
      # kitty @ launch --match id:"$kitty_window_id" --cwd=current --stdin-source=@last_cmd_output --type=overlay nvim -c "set relativenumber" -c "set buftype=nofile" -
      # TODO how do I get colors????
      # kitty @ launch --match id:"$kitty_window_id" --cwd=current --stdin-source=@last_cmd_output --type=overlay less -R --use-color
    else
      echo
      echo '‚úÖ'
      echo closing...
      sleep 2
    fi
    exit
  }
  function post_hook_0() {
    local errcode="$?"
    local title
    local body
    local prev_cmd="$(tail -n 1 $HISTFILE | sed 's/^[^;]*;//' | sed 's/; '"$0"'.*$//')"
    if [[ "$errcode" = "0" ]]; then
      title="‚úÖ $prev_cmd"
      body='yay'
    else
      title="‚ùå $prev_cmd"
      body='crap'
    fi
    printf '\x1b]99;i=1:d=0:o=invisible;'"$title"'\x1b\\'
    printf '\x1b]99;i=1:d=1:p=body;'"$body"'\x1b\\'
    if [[ "$errcode" = "0" ]]; then
      exit
    else
      printf '\e[?5h'; sleep 0.1 ; printf '\e[?5l'
      kitty @ focus-window --match id:$KITTY_WINDOW_ID
      kitty @ launch --cwd=current --stdin-source=@last_cmd_output --type=overlay bat
    fi
  }
  rk_mark() {
    kitty @ --password "$KITTY_RC_PASSWORD" create-marker --match all text 3 "$1"
  }
  rk_unmark() {
    kitty @ --password "$KITTY_RC_PASSWORD" remove-marker --match all
  }
  [[ $print_emojis ]] && echo -n üêà
fi
# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
zstyle ':completion:*:*:make:*' tag-order 'targets'

[[ $print_emojis ]] && echo -ne üïì

autoload -z edit-command-line
zle -N edit-command-line
bindkey "^X^E" edit-command-line
# https://unix.stackexchange.com/a/34251

# clear line
# echo -e "\033[1K"
[[ $print_emojis ]] && echo -ne üçë

has xattr && alias unquarantine='xattr -d com.apple.quarantine'
has ffmpeg && function mov_half_res() {
  if [[ -z "$1" ]]; then
    echo 'mov_half <input> [output]'
    return 1
  fi
  ( set -x; ffmpeg -i "$1" -vf "scale=iw/2:ih/2" "${2:-output.mp4}" )
}
has ffmpeg && function mov_double_speed() {
  if [[ -z "$1" ]]; then
    echo 'mov_half <input> [output]'
    return 1
  fi
  ( set -x; ffmpeg -i "$1" -filter:v "setpts=PTS/2,fps=24" -an "${2:-output.mp4}" )
  # setpts=PTS/2 devides each frame's presentation timestamp by 2
  # fps=24 outputs 24 frames per second
  # -an removes audio
  # (https://superuser.com/questions/1261678/how-do-i-speed-up-a-video-by-60x-in-ffmpeg)
}

